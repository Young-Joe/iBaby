apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'org.greenrobot.greendao' //greendao plugin

android {
    compileSdkVersion rootProject.ext.targetSDKVersion
    defaultConfig {
        applicationId "com.joe.ibaby"
        minSdkVersion rootProject.ext.minSDKVersion
        targetSdkVersion rootProject.ext.targetSDKVersion
        versionCode rootProject.ext.versionCode
        versionName "1.0.${rootProject.ext.versionCode}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    signingConfigs {
        release {
            Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('keystore/signing.properties')))
            storeFile file(localProps["STORE_FILE"])
            storePassword localProps["STORE_PASSWORD"]
            keyAlias localProps["KEY_ALIAS"]
            keyPassword localProps["KEY_PASSWORD"]
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release //使用同一个签名信息
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if(variant.buildType.name.equals('release')){
                //这里修改文件名 variant.productFlavors[0].name  _${getSvnRevision()}
                outputFileName = "iBaby${variant.flavorName}_V${defaultConfig.versionName}_release.apk"
            }else if(variant.buildType.name.equals('debug')){
                outputFileName = "iBaby${variant.flavorName}_V${defaultConfig.versionName}_debug.apk"
            }
        }
    }
    //编译配置
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

greendao {
    schemaVersion rootProject.ext.dbVersion
    daoPackage 'com.joe.ibaby.dao.greendao'          //指定dao目录
    targetGenDir 'src/main/java'                     //指定生成DaoMaster 、DaoSession、Dao位置
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation rootProject.ext.designCompat
    implementation rootProject.ext.cardView
    implementation project(':customlibrary')
    compile 'com.sdsmdg.tastytoast:tastytoast:0.1.1'
    compile 'com.alibaba:fastjson:1.2.33'
    //以下SDK开发者请根据需要自行选择
    //bmob-sdk：Bmob的android sdk包，包含了Bmob的数据存储、文件等服务，以下是最新的bmob-sdk:
    //3.5.5：请务必查看下面注释[1]
    compile 'cn.bmob.android:bmob-sdk:3.5.7'
    //bmob-push：Bmob的推送包
    compile 'cn.bmob.android:bmob-push:1.0.1'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.zhihu.android:matisse:0.5.0-beta3'
    compile 'com.soundcloud.android:android-crop:1.0.1@aar'

}
